plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.2' apply false
    id 'io.spring.dependency-management' version '1.1.6'
}
allprojects {
    group = 'ashen.store'
    version = '1.0.0'
    repositories { mavenCentral() }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.boot:spring-boot-dependencies:3.3.2'
            mavenBom 'org.springframework.modulith:spring-modulith-bom:1.4.2'
        }
    }

    dependencies {
        compileOnly 'org.springframework.modulith:spring-modulith-api'     // 패키지 애노테이션 컴파일용
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor' // 메타데이터

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        compileOnly 'jakarta.persistence:jakarta.persistence-api'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
    }

    test {
        useJUnitPlatform()
    }

}
